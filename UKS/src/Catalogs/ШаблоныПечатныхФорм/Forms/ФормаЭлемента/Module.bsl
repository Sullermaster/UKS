
&НаКлиенте
Процедура ПослеПомещенияФайла(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	АдресФайлаВХранилище = Адрес;
	ШаблонФайла = ВыбранноеИмяФайла;
	ЗагружаемыйФайл = Новый Файл(ВыбранноеИмяФайла);
	Объект.РасширениеФайла = Сред(ЗагружаемыйФайл.Расширение,2,СтрДлина(ЗагружаемыйФайл.Расширение)-1);
	Объект.ДатаСоздания = ЗагружаемыйФайл.ПолучитьУниверсальноеВремяИзменения();
	Объект.РазмерФайла = ЗагружаемыйФайл.Размер();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиФайла(ВыбранныйФайл,ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйФайл=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Расширение",);
	ДополнительныеПараметры.Вставить("ДатаСоздания",);
	ДополнительныеПараметры.Вставить("РазмерФайла",);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПомещенияФайла",ЭтаФорма,ДополнительныеПараметры);
	НачатьПомещениеФайла(ОписаниеОповещения,,ВыбранныйФайл[0],Ложь,УникальныйИдентификатор); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлШаблон(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	ДиалогОткрытия.Фильтр = "Файл шаблона MS-Word (*.doc)|*.doc*";
	ОписаниеОповещенияПослеЗагрузкиФайла = Новый ОписаниеОповещения("ПослеЗагрузкиФайла",ЭтаФорма);
	ДиалогОткрытия.Показать(ОписаниеОповещенияПослеЗагрузкиФайла);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФайлаВХранилище) Тогда
		ТекущийОбъект.Шаблон = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФайлаВХранилище));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		Элементы.ОбъектМетаданных.СписокВыбора.Добавить(Документ.ПолноеИмя());
	КонецЦикла;
	
КонецПроцедуры
